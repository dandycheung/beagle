apply plugin: "com.android.application"
apply from: "${rootProject.projectDir}/scripts/module-android.gradle"
apply plugin: "kotlin-parcelize"
apply plugin: "kotlin-kapt"

android {
    defaultConfig {
        applicationId "com.pandulapeter.beagle"
        versionCode 29
        versionName "1.5.1"
        buildConfigField("String", "BUILD_DATE", "\"${new Date().format("yyyy-MM-dd")}\"")
        buildConfigField("String", "BEAGLE_VERSION", "\"$libraryVersion\"")
    }
    signingConfigs {
        internal {
            keyAlias "androiddebugkey"
            keyPassword "android"
            storeFile file("internal.keystore")
            storePassword "android"
        }
        release {
            keyAlias "androiddebugkey"
            keyPassword "android"
            storeFile file("internal.keystore")
            storePassword "android"
        }
    }
    buildFeatures.dataBinding = true
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            multiDexEnabled true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            signingConfig signingConfigs.internal
        }
        internal {
            debuggable false
            minifyEnabled true
            shrinkResources true
            multiDexEnabled false
            applicationIdSuffix ".internal"
            versionNameSuffix "-internal"
            signingConfig signingConfigs.internal
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            matchingFallbacks = ["debug"]
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            multiDexEnabled false
            applicationIdSuffix ".release"
            versionNameSuffix "-release"
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    flavorDimensions "beagle-ui"
    productFlavors {
        activity {
            applicationIdSuffix ".activity"
            versionNameSuffix "-activity"
        }
        bottomSheet {
            applicationIdSuffix ".bottomSheet"
            versionNameSuffix "-bottom-sheet"
        }
        dialog {
            applicationIdSuffix ".dialog"
            versionNameSuffix "-dialog"
        }
        drawer {
            applicationIdSuffix ".drawer"
            versionNameSuffix "-drawer"
        }
        view {
            applicationIdSuffix ".view"
            versionNameSuffix "-view"
        }
    }
    lint {
        abortOnError false
    }
}

configurations {
    activityDebugImplementation
    activityInternalImplementation
    bottomSheetDebugImplementation
    bottomSheetInternalImplementation
    dialogDebugImplementation
    dialogInternalImplementation
    drawerDebugImplementation
    drawerInternalImplementation
    viewDebugImplementation
    viewInternalImplementation
}

dependencies {
    implementation "androidx.multidex:multidex:2.0.1" // https://developer.android.com/jetpack/androidx/releases/multidex
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "io.insert-koin:koin-android:3.1.6" // https://github.com/InsertKoinIO/koin/tags
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    def retrofitVersion = "2.9.0" // https://github.com/square/retrofit/tags
    implementation("com.squareup.retrofit2:retrofit:$retrofitVersion") { exclude module: "okhttp" }
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.android.billingclient:billing:4.1.0" // https://developer.android.com/google/play/billing/release-notes
    implementation "androidx.core:core-splashscreen:1.0.0-beta02" // https://developer.android.com/jetpack/androidx/releases/core
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.9.1" // https://square.github.io/leakcanary/changelog/
    implementation project(":internal-utilities")
    activityDebugImplementation project(":ui-activity")
    activityDebugImplementation project(":log")
    activityDebugImplementation project(":log-okhttp")
    activityDebugImplementation project(":log-crash")
    activityInternalImplementation project(":ui-activity")
    activityInternalImplementation project(":log")
    activityInternalImplementation project(":log-okhttp")
    activityInternalImplementation project(":log-crash")
    bottomSheetDebugImplementation project(":ui-bottom-sheet")
    bottomSheetDebugImplementation project(":log")
    bottomSheetDebugImplementation project(":log-okhttp")
    bottomSheetDebugImplementation project(":log-crash")
    bottomSheetInternalImplementation project(":ui-bottom-sheet")
    bottomSheetInternalImplementation project(":log")
    bottomSheetInternalImplementation project(":log-okhttp")
    bottomSheetInternalImplementation project(":log-crash")
    dialogDebugImplementation project(":ui-dialog")
    dialogDebugImplementation project(":log")
    dialogDebugImplementation project(":log-okhttp")
    dialogDebugImplementation project(":log-crash")
    dialogInternalImplementation project(":ui-dialog")
    dialogInternalImplementation project(":log")
    dialogInternalImplementation project(":log-okhttp")
    dialogInternalImplementation project(":log-crash")
    drawerDebugImplementation project(":ui-drawer")
    drawerDebugImplementation project(":log")
    drawerDebugImplementation project(":log-okhttp")
    drawerDebugImplementation project(":log-crash")
    drawerInternalImplementation project(":ui-drawer")
    drawerInternalImplementation project(":log")
    drawerInternalImplementation project(":log-okhttp")
    drawerInternalImplementation project(":log-crash")
    viewDebugImplementation project(":ui-view")
    viewDebugImplementation project(":log")
    viewDebugImplementation project(":log-okhttp")
    viewDebugImplementation project(":log-crash")
    viewInternalImplementation project(":ui-view")
    viewInternalImplementation project(":log")
    viewInternalImplementation project(":log-okhttp")
    viewInternalImplementation project(":log-crash")
    releaseImplementation project(":noop")
    releaseImplementation project(":log-noop")
    releaseImplementation project(":log-okhttp-noop")
    releaseImplementation project(":log-crash-noop")
}